&CONTROL OFF NOMSG
 
*
* GET THE PATTERN
*
&PFN = *
&PFT = *
&PFM = A
&IF .&1 NE . &PFN = &1
&IF .&2 NE . &PFT = &2
&IF .&3 NE . &PFM = &3
 
*
* GET THE R/W DISK
*
&RWDISK = &DISK*
&IF &RWDISK NE NONE &GOTO -RWOK
&TYPE ** XLIST ERROR : NO R/W DISK ACCESSED
&EXIT 24
-RWOK
 
*
* FIND A UNIQUE EXEC FILENAME TO USE FOR XLIST FILE ACTIONS
*
&I = 0
&OK = 0
*
&LOOP -FX &OK NE 0
  &I = &I + 1
  &EXFN = $$XLE&I
  STATE &EXFN EXEC *
  &RC = &RETCODE
  &IF &RC NE 28 &GOTO -FXFND
  STATE &EXFN XLISTRES *
  &RC = &RETCODE
  &IF &RC NE 28 &GOTO -FXFND
  &OK = 1
  &GOTO -FXNXT
  -FXFND
  &IF &RC EQ 0 &GOTO -FXNXT
  &TYPE ** XLIST ERROR : CANNOT FIND TEMP EXEC NAME ON R/W DISK
  &EXIT 24
  -FXNXT
-FX
*
*&TYPE TEMP EXEC : &EXFN EXEC &RWDISK
 
*
* DO THE XLIST LOOP: XLIST >> &EXFN >> XLIST >> ...
* WHILE XLIST RETURNS 2044 (EXECUTE COMMANDS EXEC)
*
&XLMODE = XLISTS
-XLLOOP
  *&TYPE EE &PFN &PFT &PFM ( &XLMODE &EXFN EXEC &RWDISK
  EE &PFN &PFT &PFM ( &XLMODE &EXFN EXEC &RWDISK
  &RC = &RETCODE
  &IF &RC NE 2044 &GOTO -XLDONE
  EXEC &EXFN
  &XLMODE = XLISTR
  &GOTO -XLLOOP
*
-XLDONE
STATE &EXFN EXEC &RWDISK
&IF &RETCODE EQ 0 ERASE &EXFN EXEC &RWDISK
STATE &EXFN XLISTRES &RWDISK
&IF &RETCODE EQ 0 ERASE &EXFN XLISTRES &RWDISK
&EXIT &RC
